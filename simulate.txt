
    model{
    # PRIORS
    for(j in 1:NJ){
    spp.mu[j] ~ dnorm(0, 1e-6)
    lambda[j] ~ dunif(0.01, 10)
    eta[j] ~ dunif(1e-6, 10)
    }
    
    for(i in 1:NI){
    for(j in 1:NJ){
    mu[i,j] <- spp.mu[j]
    }}
    
    # COVARIANCE MATRIX
    for(i in 1:NI){
    for(c in i:NI){
    for(j in 1:NJ){
    M.cov[i,c,j] <- eta[j] * exp(-dist[i,c] / lambda[j])
    }}}
    
    for(i in 1:NI){
    for(c in (i+1):NI){
    for(j in 1:NJ){
    M.cov[c,i,j] <- M.cov[i,c,j]
    }}}
    
    M.tau[1:NI,1:NI,1] <- inverse(M.cov[,,1])
    M.tau[1:NI,1:NI,2] <- inverse(M.cov[,,2])
    M.tau[1:NI,1:NI,3] <- inverse(M.cov[,,3])
    
    # LIKELIHOOD
    for(j in 1:NJ){
    g[1:NI,j] ~ dmnorm(mu[1:NI,j], M.tau[,,j])
    }
    
    for(i in 1:NI){
    for(j in 1:NJ){
    r[i,j] <- exp(g[i,j]) / (exp(g[i,1]) + exp(g[i,2]) + exp(g[i,3]))
    }}
    
    for(i in 1:NI){
    Y[i,1:NJ] ~ dmulti(r[i,1:NJ], Sum_counts[i])
    }
    
    #data# Y, Sum_counts, NJ, NI, dist
    #monitor# lambda, eta, g, spp.mu
    }